'
' GRAF_SMP.GFA
'
REPEAT
  CLS
  PRINT CHR$(27)+"p";
  PRINT "|  <F1> rubber  |  <F2> drag  |  <F3> move  |  <F4> grow_shrink  |  <F10> quit |"
  PRINT AT(1,2);CHR$(27);"qF1 and F2: Use mouse with left button. Exit with right mouse button"
  choice|=INP(2)
  '
  SELECT choice|
  CASE 187      ! F1
    rubber
  CASE 188      ! F2
    drag
  CASE 189      ! F3
    move
  CASE 190      ! F4
    grow_shrink
  ENDSELECT
  '
  PRINT CHR$(27)+"q";
  '
UNTIL choice|=196 ! quit with F10
'
EDIT
'
PROCEDURE rubber
  GRAPHMODE 3
  DEFFILL 1,2,4
  REPEAT
    MOUSE mx%,my%,mk%
    IF mk% AND 1
      x1%=mx%
      y1%=my%
      ~GRAF_RUBBERBOX(x1%,y1%,16,16,lx%,ly%)
      PBOX x1%,y1%,x1%+lx%,y1%+ly%
    ENDIF
  UNTIL mk% AND 2
RETURN
'
PROCEDURE drag
  GRAPHMODE 1
  DEFFILL 1,2,4
  REPEAT
    MOUSE mx%,my%,mk%
    BOX 40,40,440,340
    IF mk% AND 1
      ~GRAF_DRAGBOX(150,100,mx%,my%,40,40,400,300,lx%,ly%)
      PBOX lx%,ly%,lx%+150,ly%+100
    ENDIF
  UNTIL mk% AND 2
RETURN
'
PROCEDURE move
  GRAPHMODE 1
  DEFFILL 1,2,4
  b%=100
  h%=100
  FOR i%=0 TO 639-b% STEP b%
    FOR j%=0 TO 399-h% STEP h%
      ~GRAF_MOVEBOX(b%,h%,i%,j%,639-i%,399-j%)
    NEXT j%
  NEXT i%
RETURN
'
PROCEDURE grow_shrink
  GRAPHMODE 1
  ~GRAF_GROWBOX(319,199,16,16,0,0,639,399)
  ALERT 0,"That was a graf_growbox !",1,"Continue",r%
  ~GRAF_SHRINKBOX(319,199,16,16,0,0,639,399)
  ALERT 0,"That was a graf_shrinkbox !",1,"Yes",r%
RETURN
'
' See GFA-BASIC 3 reference manual chapter 11 for full explanation.
