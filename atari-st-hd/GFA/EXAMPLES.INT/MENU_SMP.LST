' ** Menu Demo Program
' This demo loads in the RCS file of the RCS program to show how to
' access menus.
' Resource reserve, Resource load
'
RESERVE FRE(0)-33000
IF RSRC_LOAD("A:\RCS\RCS2.RCS")=0
  ~FORM_ALERT(1,"[1] RCS2.RCS not found!|Check path name][Abort]")
  END
ENDIF
~RSRC_GADDR(0,0,menu_adr%)
~MENU_BAR(menu_adr%,1)
'
' Message-Buffer
DIM message_buffer%(3)
mes_adr%=V:message_buffer%(0)
ABSOLUTE mes_type&,mes_adr%
ABSOLUTE m_title&,mes_adr%+6
ABSOLUTE m_entry&,mes_adr%+8
'
REPEAT
  ~EVNT_MULTI(&X110000,0,0,0,0,0,0,0,0,0,0,0,0,0,mes_adr%,500)
  IF mes_type&=10
    IF obj_nr%<>m_entry&
      obj_nr%=m_entry&
      titel$=CHAR{OB_SPEC(menu_adr%,m_title&)}
      entry$=CHAR{OB_SPEC(menu_adr%,m_entry&)}
      PRINT AT(3,20);"Menu Title:    ";titel$;SPC(10)
      PRINT AT(3,22);"Menu Entry:   ";entry$;SPC(10)
      ~MENU_TNORMAL(menu_adr%,m_title&,1)
    ENDIF
  ENDIF
UNTIL MOUSEK=2
'
~MENU_BAR(menu_adr%,0)
~RSRC_FREE()
RESERVE FRE(0)+33000
END
